(* TODO(JS/YAP): Are node removals logged? *)
LET relevant_node(node, subnet) = ONCE p2p__node_added(node, subnet) IN
LET num_relevant_nodes(subnet, n) = n <- CNT node; subnet
  ONCE p2p__node_added(node, subnet) IN

LET deliver_block_proposal(reporting_node, subnet, block_hash, signer) =
  move_block_proposal(reporting_node, subnet, block_hash, signer)
  AND EVENTUALLY [0,600s] deliver_batch(reporting_node, subnet, block_hash)
IN
LET num_created_one_day_raw(reporting_node, subnet, signer, n) =
  n <- CNT block_hash; reporting_node, subnet, signer
    ONCE [0, 1d (*](*) )
      deliver_block_proposal(reporting_node, subnet, block_hash, signer)
IN
LET num_created_one_day(reporting_node, subnet, signer, n) =
  num_created_one_day_raw(reporting_node, subnet, signer, n)
  OR (relevant_node(reporting_node, subnet)
      AND relevant_node(signer, subnet)
      AND NOT num_created_one_day_raw(reporting_node, subnet, signer, _)
      AND n = 0)
IN

LET avg_created_one_day(reporting_node, subnet, a) =
  a <- AVG n; reporting_node, subnet
    num_created_one_day(reporting_node, subnet, node, n)
IN
(* Population variance: *)
LET sqsum_created_one_day(reporting_node, subnet, s) =
  s <- SUM nsq; reporting_node, subnet
    (EXISTS n. num_created_one_day(reporting_node, subnet, signer, n)
               AND nsq = n * n)
IN
LET varp_created_one_day(reporting_node, subnet, v) =
  EXISTS a, b, n.
    avg_created_one_day(reporting_node, subnet, a)
    AND sqsum_created_one_day(reporting_node, subnet, b)
    AND num_relevant_nodes(subnet, n)
    AND v = (i2f(b) / i2f(n)) - (a * a)
IN

num_created_one_day(reporting_node, subnet, creating_node, n_created)
AND avg_created_one_day(reporting_node, subnet, avg_created)
AND varp_created_one_day(reporting_node, subnet, var_created)
AND (i2f(n_created) - avg_created) * (i2f(n_created) - avg_created)
    > 4.0 * var_created  (* variance = stddev^2 *)